import java.util.regex.Matcher
import java.util.regex.Pattern

buildscript {
    ext.kotlin_version = "1.7.20"
    ext.dagger_version = '2.44'
    ext.plugin_version = '7.3.1'

    ext {
        compose_ui_version = '1.3.2'
    }
    //region Load Flavors Configuration
    ext.loadPropertiesFromFile = { fileName->
        def propertiesFile = rootProject.file(fileName)
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))
        return properties
    }

    ext.getCurrentFlavor = {
        Gradle gradle = getGradle()
        def pattern = Pattern.compile("([A-Z][A-Za-z]+)(Release|Debug)")
        def flavor = ""

        gradle.getStartParameter().getTaskNames().any() { name ->
            Matcher matcher = pattern.matcher(name)
            if (matcher.find()) {
                flavor = matcher.group(1).toLowerCase()
                return true
            }
        }
        return flavor
    }

    ext {
        flavor = getCurrentFlavor()
        if (flavor.isEmpty()) {
            flavor = "dev"
        }
        config = loadPropertiesFromFile('config/' + flavor + '.properties')
    }

    //endregion
    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_version"
        classpath "com.android.tools.build:gradle:$plugin_version"
        classpath 'com.google.firebase:perf-plugin:1.4.2'
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version "${plugin_version}" apply false
//    id 'com.android.library' version "${plugin_version}" apply false
    id 'org.jetbrains.kotlin.android' version "${kotlin_version}" apply false
    id 'com.google.gms.google-services' version '4.3.13' apply false

}